isEmpty(TARGET): error(set TARGET first)
!contains(TARGET, ^dtk[a-z0-9]+$): error(The format of the value of TARGET is incorrect. eg: dtk[a-z0-9]+)
!isEqual(TARGET, dtkcore):!contains(QT, dtkcore): error(QT += dtkcore first)

defineTest(checkDtkVersion) {
    isEmpty(VERSION) {
        !isEqual(TARGET, dtkcore) {
            VERSION = $${QT.dtkcore.VERSION}
        }

        #TODO: faild in window
        isEmpty(VERSION): VERSION = $$system(git describe --tags --abbrev=0)
        isEmpty(VERSION): VERSION = $$DTK_VERSION
        isEmpty(VERSION): return(false)
        VERSION = $$replace(VERSION, [^0-9.],)
    }

    export(VERSION)
    return(true)
}

!checkDtkVersion():error("check dtk version failed")

message("build dtk with version $$VERSION")
ver_list = $$split(VERSION, .)

isEmpty(VER_MAJ) {
    VER_MAJ = $$first(ver_list)
}
isEmpty(VER_MIN) {
    VER_MIN = $$member(ver_list, 1, 1)
    isEmpty(VER_MIN):VER_MIN = 0
}
isEmpty(VER_PAT) {
    VER_PAT = $$member(ver_list, 2, 2)
    isEmpty(VER_PAT):VER_PAT = 0
}
isEmpty(VER_BUI) {
    VER_BUI = $$member(ver_list, 3, 3)
    isEmpty(VER_BUI):VER_BUI = 0
}

TEMPLATE = lib

CONFIG += c++11 create_pc create_prl no_install_prl
CONFIG += no_keywords

DEFINES += QT_MESSAGELOGCONTEXT
DEFINES += LIB$$upper($$TARGET)_LIBRARY

isEmpty(PREFIX){
    PREFIX = /usr
}

isEmpty(LIB_INSTALL_DIR) {
    isEqual(TARGET, dtkcore) {
        LIB_INSTALL_DIR=$$PREFIX/lib
    } else {
        LIB_INSTALL_DIR=$${QT.dtkcore.libs}
    }
}

isEmpty(target.path): target.path = $$LIB_INSTALL_DIR

isEmpty(INCLUDE_INSTALL_DIR) {
    isEqual(TARGET, dtkcore) {
        INCLUDE_INSTALL_DIR = $$PREFIX/include/libdtk-$${VER_MAJ}.$${VER_MIN}.$${VER_PAT}
    } else {
        INCLUDE_INSTALL_DIR = $${QT.dtkcore.includes}/..
    }
}

DTK_INCLUDEPATH = $$INCLUDE_INSTALL_DIR
isEmpty(includes.path): includes.path = $$quote($$DTK_INCLUDEPATH/D$$upper($$member($$list($$split(TARGET,)), 3, 3))$$join($$list($$member($$list($$split(TARGET,)), 4, -1))),)

!isEmpty(DTK_STATIC_LIB) {
    DEFINES += DTK_STATIC_LIB
    CONFIG += staticlib
}

!isEmpty(DTK_STATIC_TRANSLATION) {
    DEFINES += DTK_STATIC_TRANSLATION
}
